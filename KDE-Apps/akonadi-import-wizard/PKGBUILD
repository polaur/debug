# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=akonadi-import-wizard
pkgver=18.12.1
pkgrel=1
pkgdesc="Import data from other mail clients to KMail"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(mailcommon)
makedepends=(extra-cmake-modules boost kdoctools python)
optdepends=('kdepim-addons: additional import plugins')
conflicts=('kmail<16.11')
option=(debug !strip)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('447365e8177959cb0c94d1745b9e896987d723d2672a6989a0a5c81cdf6acd4e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Debug
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
