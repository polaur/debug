# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=oxygen-icons
pkgname=('oxygen-icons'
         'oxygen-icons-svg')
pkgver=5.54.0
epoch=1
pkgrel=1
pkgdesc="The Oxygen Icon Theme"
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
makedepends=('extra-cmake-modules' 'qt5-base')
option=(debug !strip)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}5-${pkgver}.tar.xz"{,.sig})
sha256sums=('256a3366374f82040f1a087c70ddf77ab844b9589ef85010f73e8b6e844ead7a'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgbase}5-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Debug
  make
}

package_oxygen-icons() {
  groups=('kf5')

  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-icons-svg() {
  pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"

  cd ${pkgbase}5-${pkgver}
  find scalable -type f ! -name '*.sh' -exec \
    install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
