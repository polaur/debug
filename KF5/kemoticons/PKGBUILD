# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kemoticons
pkgver=5.54.0
pkgrel=1
pkgdesc='Support for emoticons and emoticons themes'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('karchive' 'kservice')
makedepends=('extra-cmake-modules' 'python' 'mesa' 'doxygen' 'qt5-tools')
option=(debug !strip)
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('8ad694c4ec36a261ecc6ab7bc65e413e876747ab27e2ab5e9f072421f4f7fcbd'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON \
    -DCMAKE_BUILD_TYPE=Debug
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
