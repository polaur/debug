# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktextwidgets
pkgver=5.54.0
pkgrel=1
pkgdesc='Advanced text editing widgets'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kcompletion' 'kservice' 'kiconthemes' 'sonnet' 'qt5-speech')
makedepends=('extra-cmake-modules' 'python' 'doxygen' 'qt5-tools')
option=(debug !strip)
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('97e32590cd9078a2c8e5431aa9d4b1b7dd3b71bbeef4d053aa3afe976c1ab0cf'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON \
    -DCMAKE_BUILD_TYPE=Debug
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
