# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libksieve
pkgver=18.12.1
pkgrel=1
pkgdesc="KDE PIM library for managing sieves"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kidentitymanagement pimcommon kmailtransport qt5-webengine)
makedepends=(extra-cmake-modules python boost kdoctools)
conflicts=('libkdepim<16' kio-pim)
option=(debug !strip)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('631091d0a931d4fdfaf66c5331f10a7b2f8e9edbc1805fb8d5aeb42eb85be1b2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Debug
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
